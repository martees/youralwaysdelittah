=== amaziza_default_intro ===

{amazizaMoodSwings:
    - low: -> default_intro_low ->
    - joyful: -> default_intro_joyful ->
    - depressed: -> default_intro_depressed ->
}

->->

= default_intro_low

{~Come on|Let's go|Be brave} Ziza, {~try to do|I'm sure you can do} at least one more thing before the end of the day!

Pfff... {No, I can't|Okay stop}, enough, {~I quit|I'm gonna end that here|I'm gonna stop there}. {~Fuck|Too bad for} productivity.

->->

= default_intro_joyful

Today was {~just the perfect day|sooo perfect|the best day ever}, {~I am so happy!|I really love my life!|I love life so much!}

[i]She starts to sing {~enthusiastically|with a powerful voice|with the clearest voice}.[/i]

->->

= default_intro_depressed

Pffff... {~Work, papers, chores, chores, work, papers|There is always something to do|Not even one second to breathe|Too many things to do}, how can {~anybody|anyone} live like this?

{~There is no way I can|I will never} {~stand|keep up like this|keep myself up|maintain the rate} until the holidays...

->->

=== amaziza_default_event ===

{amazizaMoodSwings:
    - low: -> default_low ->
    - joyful: -> default_joyful ->
    - depressed: -> default_depressed ->
}

->->

= default_low

{cycle:
    - I'm so {~sick of it|bored}... {~Work, papers, chores, chores, work, papers|There is always something to do|Not even one second to breathe|Too many things to do}, how can {~anybody|anyone} live like this?

    What can I do anyway, I have to {~stand|keep up|keep myself up|maintain the rate} until the holidays... 

    - {shuffle:
        - I hope {~that I'll have a quick answer|they will be reactive} for this year's high school administrative {~stuff|resquests|applications}.

        I don't want to wait {~another month|an entire month|for weeks} before {~knowing how I will|I know how to} get {~the June scholar trip|this spring's mathemagical circle of conferences|my second semester classes} organized.

        - I hope at least {~that I'll have a quick answer|they will be reactive} for next year's {~Direction of Education stuff|Board of Education resquests|social security applications|social {~safety|insurance|welfare|protection}} applications.

        I don't want to wait {~another six months|an entire semester|for months} before knowing how {~I'll|to} get my next year's {~classes|timetable} organized.
    }
}

->->

= default_joyful 

{cycle:
    - {~I sat at the table of a café,|Working peacefully, sat at a café} under the warm heat of the autumn sun. {~Is there anything better in the world?|I found no more enjoyable way to work so far.|Nothing more {~pleasing|pleasant} to {~enjoy|make the most of} the {~weather|season}.}

    I even {~achieved|finished} {~my first|my last|another} {~pile of corrections|stack of {~tests|homeworks}|stack of {~test|exam|student|} papers|math essay for the {~students|semester}}!

    - {~It is so nice to feel good!|How I love to feel good!|I feel so alive!}

    I know I have to appreciate it when I'm high, for it can {~switch|change} so {~briefly|quickly|suddenly}.
}

->->

= default_depressed 

{cycle:
    - I{~ just cannot|'ll just never} make it, this is {~too difficult|too hard|impossible}...

    [i]{~Starts to weep silently|Bursts into mute tears|Begins to sob in silence}.[/i]

    - {~Come on Ziza|Take it easy}, {~come on|you know it will do|you know it's juste a phase|you know you'll do it eventually|let it go a little}... 

    [i]{~Bursts into tears again|Cries out loud}.[/i]
    
}

->->

=== amaziza_default_sleep ===

{amazizaMoodSwings:
    - low: -> default_low ->
    - joyful: -> default_joyful ->
    - depressed: -> default_depressed ->
}

->->

= default_low

{~I'm so tired|Today was really  difficult|Today was not easy}. At least {~the house is not obviously messy|my courses for tomorrow are ready|I {~completed|sent} the administrative file for my disability aids}.

Let's just go {~to bed|to sleep}.

->->

= default_joyful

{~Come on|Okay|All right}, tonight I {~offer myself the time|give myself the right} to read a little.

I don't do it {~often enough|as much as I'd like}, {~I really love this book|where was I already|this book is so great}...

->->

= default_depressed

I'm so tired{~ of myself| of life|}...

{~Fuck|Forget} everything, I just wanna {~go to sleep|disconnect|run away|fly away}...

->->